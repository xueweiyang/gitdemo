buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.5.1'
    classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
  }
}

plugins {
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'java-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'
apply plugin: 'idea'
apply plugin: 'build-time-tracker'

buildtimetracker {
  reporters {
    csv {
      output "times.csv"
      append true
      header false
    }

    summary {}
  }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
  repositories {
    mavenCentral()
  }

  compile gradleApi()
  compile localGroovy()
  compile 'org.rauschig:jarchivelib:0.6.0'
  compile 'commons-io:commons-io:2.4'
  compile 'net.sf.opencsv:opencsv:2.3'
  compile 'org.ocpsoft.prettytime:prettytime:3.2.5.Final'
  compile 'joda-time:joda-time:2.4'

  testCompile 'junit:junit:4.11'
}

group = 'net.rdrei.android.buildtimetracker'
version = '0.12.0-SNAPSHOT'

install {
  repositories.mavenInstaller {
    pom.artifactId = 'gradle-plugin'
  }
}

compileGroovy {
  groovyOptions.forkOptions.jvmArgs = ['-noverify']
}

test {
  jvmArgs '-noverify'
}

uploadArchives {
  repositories.mavenDeployer {
    pom.artifactId = 'gradle-plugin'
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  }
}

modifyPom {
  project {
    name 'Gradle Build Time Tracker'
    description 'Gradle plugin which tracks your build times.'
    url 'https://github.com/passy/build-time-tracker-plugin'
    inceptionYear '2014'

    scm {
      url 'https://github.com/passy/build-time-tracker-plugin'
      connection 'scm:git:git://github.com/passy/build-time-tracker-plugin.git'
      developerConnection 'scm:git:ssh://git@github.com/passy/build-time-tracker-plugin.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'passy'
        name 'Pascal Hartig'
        email 'phartig@rdrei.net'
      }
    }
  }
}

gradlePlugin {
  plugins {
    buildTimeTrackerPlugin {
      id = group
      implementationClass = 'net.rdrei.android.buildtimetracker.BuildTimeTrackerPlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/passy/build-time-tracker-plugin'
  vcsUrl = 'https://github.com/passy/build-time-tracker-plugin.git'
  description = 'Gradle plugin to continuously track and report your build times.'
  tags = ['gradle', 'performance', 'buildtimes', 'matrics']

  plugins {
    buildTimeTrackerPlugin {
      displayName = 'Build Time Tracker'
    }
  }
}
